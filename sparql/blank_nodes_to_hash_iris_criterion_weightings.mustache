PREFIX pc: <http://purl.org/procurement/public-contracts#>

DELETE {
  ?bnode ?outP ?outO .
  ?inS ?inP ?bnode .
}
INSERT {
  ?iri ?outP ?outO .
  ?inS ?inP ?iri .
}
WHERE {
  {
    SELECT ?bnode ?iri
    WHERE {
      {
        SELECT ?bnode
               (SHA1(CONCAT(GROUP_CONCAT(STR(?p); separator = ""),
                            GROUP_CONCAT(STR(?o); separator = ""))) AS ?hash)
                            WHERE {
          {
            SELECT ?bnode
            WHERE {
              {
                SELECT ?bnode
                WHERE {
                  ?bnode a pc:CriterionWeighting .
                  FILTER isBlank(?bnode)
                }
                ORDER BY ?bnode
              }
            }
            LIMIT {{limit}}
            OFFSET {{offset}}
          }
          ?bnode ?p ?o .
        }
        GROUP BY ?bnode
      }
      BIND ("http://linked.opendata.cz/resource/" AS ?ns)
      BIND (IRI(CONCAT(?ns, "criterion-weighting/", ?hash)) AS ?iri)
    }
  }
  ?bnode ?outP ?outO .
  OPTIONAL {
    ?inS ?inP ?bnode .
  }
}

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX isvz:    <http://linked.opendata.cz/ontology/isvz.cz/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

DELETE {
  ?notice ?p ?o .
}
INSERT {
  ?contract ?p ?o .
}
WHERE {
  {
    {
      SELECT ?contract ?notice
      WHERE {
        {
          SELECT ?contract (SAMPLE(?_notice) AS ?notice)
          WHERE {
            ?contract isvz:notice ?_notice .
            ?_notice ?p [] .
            FILTER (?p NOT IN (dcterms:dateSubmitted, dcterms:issued, dcterms:type, rdf:type))
          }
          GROUP BY ?contract
          HAVING (COUNT(DISTINCT ?_notice) = 1)
          ORDER BY ?contract
        }
      }
      LIMIT {{limit}}
    }
  }
  ?notice ?p ?o .
  FILTER (?p NOT IN (dcterms:dateSubmitted, dcterms:issued, dcterms:type, rdf:type))
}
